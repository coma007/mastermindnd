package userActivity;

import com.ftn.sbnz.model.events.enums.AddCampaignType
import com.ftn.sbnz.model.events.AddCampaignEvent
import java.util.List
import java.util.Map
import com.ftn.sbnz.model.models.enums.Level;
import com.ftn.sbnz.model.models.User;
import com.ftn.sbnz.model.models.UserActivity;
import com.ftn.sbnz.model.models.Campaign;

import static java.util.stream.Collectors.*
import java.util.HashMap
import com.ftn.sbnz.model.models.enums.GameplayStyle
import com.ftn.sbnz.model.models.enums.Theme;

rule "save campaign"
salience 10
no-loop when
    AddCampaignEvent(
    $user: user,
    $campaign: campaign,
    type == AddCampaignType.SAVE
    )
    $wishlist: UserActivity() from $user.getWishlist()
then
    modify($wishlist) {
        addCampaign($campaign)
    };
end

rule "like campaign"
salience 5
no-loop when
    AddCampaignEvent(
    $user: user,
    $campaign: campaign,
    type == AddCampaignType.LIKE
    )
    $preference: UserActivity() from $user.getPreference()
then
    modify($preference) {
        addCampaign($campaign)
    };
end

rule "play campaign"
salience 5
no-loop when
    AddCampaignEvent(
    $user: user,
    $campaign: campaign,
    type == AddCampaignType.PLAY
    )
    $history: UserActivity() from $user.getHistory()
then
    modify($history) {
        addCampaign($campaign)
    };
end

rule 'is ready for recommendation from history'
salience 3
when
    AddCampaignEvent(
    $user: user,
    type == AddCampaignType.PLAY
    )
    $history: UserActivity(
    $playedCampaigns: getCampaigns(),
    $level: getBestLevel(),
    $style: getBestStyle(),
    $theme: getBestTheme()
    ) from $user.getHistory()
    Number(intValue % 2 == 0, intValue != 0) from $playedCampaigns.size()
    $campaigns : List() from collect(Campaign())
then
    Campaign cam = null;
    for (Object o : $campaigns) {
        Campaign c = (Campaign) o;
        if (c.getGameplayStyle() == $style
        && c.getLevel() == $level
        && c.getTheme() == $theme)
            {
                cam = c;
                break;
            }
    }
    modify($user){recommendNew(cam)}
end

rule 'is ready for recommendation from wishlist'
salience 3
no-loop when
    AddCampaignEvent(
    $user: user,
    $campaign: campaign,
    type == AddCampaignType.SAVE
    )
    $wishlist: UserActivity(
        $savedCampaigns: getCampaigns(),
        $level: getBestLevel(),
        $style: getBestStyle(),
        $theme: getBestTheme()
    ) from $user.getWishlist()
    Number(intValue % 2 == 0, intValue != 0) from $savedCampaigns.size()
    $campaigns : List() from collect(Campaign())
then
    Campaign cam = null;
    for (Object o : $campaigns) {
        Campaign c = (Campaign) o;
        if (c.getGameplayStyle() == $style
        && c.getLevel() == $level
        && c.getTheme() == $theme)
            {
                cam = c;
                break;
            }
    }
    modify($user){recommendNew(cam)}
end

rule 'is ready for recommendation from preference'
salience 3
no-loop when
    AddCampaignEvent(
    $user: user,
    $campaign: campaign,
    type == AddCampaignType.LIKE
    )
    $preference: UserActivity(
        $likedCampaigns: getCampaigns(),
        $level: getBestLevel(),
        $style: getBestStyle(),
        $theme: getBestTheme()
    ) from $user.getPreference()
    Number(intValue % 2 == 0, intValue != 0) from $likedCampaigns.size()
    $campaigns : List() from collect(Campaign())
then
    Campaign cam = null;
    for (Object o : $campaigns) {
        Campaign c = (Campaign) o;
        if (c.getGameplayStyle() == $style
        && c.getLevel() == $level
        && c.getTheme() == $theme)
            {
                cam = c;
                break;
            }
    }
    modify($user){recommendNew(cam)}
end

rule 'playing a lot of the same difficulty in the last month, here is some more'
no-loop when
    $user: User()
    $history: UserActivity(recommendedThisMonth == false) from $user.getHistory()
    $campaigns : List() from collect(Campaign())
    $e: AddCampaignEvent(
        $user == user,
        $campaign: campaign,
        type == AddCampaignType.PLAY
        )
        $level: Level() from $campaign.getLevel()
    Number(intValue > 1) from accumulate(
        $event: AddCampaignEvent(
        $user == user,
        $level == campaign.getLevel(),
        type == AddCampaignType.PLAY,
        $e != this, this after[2s, 30d] $e),
        count($event)
    )
then
    Campaign cam = null;
    for (Object o : $campaigns) {
        Campaign c = (Campaign) o;
        if (c.getLevel() == $level)
            {
                cam = c;
                break;
            }
    }
    modify($user){recommendNew(cam)}
    modify($history){setRecommendedThisMonth(true)};
end

rule 'playing a lot of the same theme in the last month, here is some more'
no-loop when
    $user: User()
    $history: UserActivity(recommendedThisMonth == false) from $user.getHistory()
    $campaigns : List() from collect(Campaign())
    $e: AddCampaignEvent(
        $user == user,
        $campaign: campaign,
        type == AddCampaignType.PLAY
        )
        $theme: Theme() from $campaign.getTheme()
        Number(intValue > 1) from accumulate(
        $event: AddCampaignEvent(
        $user == user,
        $theme == campaign.getTheme(),
        type == AddCampaignType.PLAY,
        $e != this, this after[2s, 30d] $e),
        count($event)
    )
then
    Campaign cam = null;
    for (Object o : $campaigns) {
        Campaign c = (Campaign) o;
        if (c.getTheme() == $theme)
            {
                cam = c;
                break;
            }
    }
    modify($user){recommendNew(cam)}
    modify($history){setRecommendedThisMonth(true)};
end


rule 'playing a lot of the same style in the last month, here is some more'
no-loop when
    $user: User()
    $history: UserActivity(recommendedThisMonth == false) from $user.getHistory()
    $campaigns : List() from collect(Campaign())
    $e: AddCampaignEvent(
        $user == user,
        $campaign: campaign,
        type == AddCampaignType.PLAY
        )
        $style: GameplayStyle() from $campaign.getGameplayStyle()
    Number(intValue > 1) from accumulate(
        $event: AddCampaignEvent(
        $user == user,
        $style == campaign.getGameplayStyle(),
        type == AddCampaignType.PLAY,
        $e != this, this after[2s, 30d] $e),
        count($event)
    )
then
    Campaign cam = null;
    for (Object o : $campaigns) {
        Campaign c = (Campaign) o;
        if (c.getGameplayStyle() == $style)
            {
                cam = c;
                break;
            }
    }
    modify($user){recommendNew(cam)}
    modify($history){setRecommendedThisMonth(true)};
end

rule 'new campaign with the same spirit'
when
    $campaign: Campaign()
    $user: User()
    $preference: UserActivity(
    $preferredLevels: getLevels(),
    $preferredStyles: getStyles(),
    $preferredThemes: getThemes()
    ) from $user.getPreference()
    eval(
        $preferredLevels.contains($campaign.getLevel()) ||
        $preferredStyles.contains($campaign.getGameplayStyle()) ||
        $preferredThemes.contains($campaign.getTheme())
    )
then
    modify($user){recommendNew($campaign)}
end